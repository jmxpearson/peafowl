model { #for use on peahen data
      	#negative binomial model of looking data
  
  	# standard deviation/precision for male variability
	mstd ~ dunif(0,4)
	mprec <- pow(mstd,-2)

  	# standard deviation/precision of female variability
	fstd ~ dunif(0,4)
	fprec <- pow(fstd,-2)

  	# random effects for males
	for (ind in 1:nmale){
	    mval[ind] ~ dnorm(0,mprec)
	}

  	#random effects for females
	for (ind in 1:nfem){
	    fval[ind] ~ dnorm(0,fprec)
	}

  	# parameters for each ROI
	for (ind in 1:nroi){
	    beta[ind] ~ dnorm(0,1e-2)
	    r[ind] ~ dunif(0.001,200) #dgamma(0.01,0.01)
	    lp[ind] ~ dunif(0,1) #probability count is nonzero
	}

  	# loop over trials
	for (ind in 1:length(ntot)){
      
            # where or not there's a fixation is a coin flip
	    didlook[ind] ~ dbern(lp[roi[ind]]) 
      
    	    # as described in the paper, pixels and total looking time are constant 
    	    # offsets; male and female effects combine with ROI effects
	    log(lambda[ind]) <- log(ntot[ind]) + log(pix[ind]) + mval[male[ind]] + fval[fem[ind]] + beta[roi[ind]]

    	    # parameter for negative binomial
	    p[ind] <- r[roi[ind]]/(r[roi[ind]] + max(1e-8,didlook[ind]) * lambda[ind]) #as per jags convention

            # draw fixation counts (data)
	    count[ind] ~ dnegbin(p[ind],r[roi[ind]])
	   
            # generate simulated data
	    z[ind] ~ dbern(lp[roi[ind]])
	    n[ind] ~ dnegbin(p[ind],r[roi[ind]])  #fake data
	    N[ind] <- z[ind]*n[ind]
	}
	
	#now draw some things that are useful
        #normalize reported betas to sum to 0
	B <- beta - mean(beta)	
	M <- mean(beta)

}